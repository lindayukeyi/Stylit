python( "import maya.OpenMaya as om" );

global int $numCustom = 0;
global string $exemplarPath;
global string $lpePath;
print("melPath: " + $melPath);
 
global proc browseFiles()
{
 	global string $lpePath;
	string $filePath[] = `fileDialog2 -ds 2 -fm 3 -okc "Open"`;
	$lpePath = $filePath[0];
	textField -edit -tx $lpePath myLPEs;
}

global proc clearLPEs()
{
	global string $lpePath;
	$lpePath = "";
	textField -edit -tx "" myLPEs;
}

global proc AddExemplars()
{
 	string $customSample[] = `fileDialog2 -fm 4`;
	global int $numCustom;
	for ($item in $customSample)
	{
		$numCustom = $numCustom + 1;
		string $customSampleName = "Custom"+string($numCustom);
		window ExemplarWindow -edit;
			rowLayout -numberOfColumns 5;
				iconTextRadioCollection itRadCollection -edit;
				iconTextRadioButton -st "iconOnly" -image1 $item -label $customSampleName -w 100 -h 100;
	}
}

global proc LoadExemplars()
{
	string $selectedBtn = `iconTextRadioCollection -q -select itRadCollection`;
	string $exemplarName = `iconTextRadioButton -q -label $selectedBtn`;
	global string $exemplarPath;
	$exemplarPath = `iconTextRadioButton -q -image1 $selectedBtn`;
	textField -edit -tx $exemplarName myExemplar;
	image -edit -image $exemplarPath myExemplarImg;
}

global proc ExemplarGUI()
{
	global string $melPath;
	string $sample1 = $melPath + "/images/source/source_style.jpg";
	string $sample2 = $melPath + "/images/source/source_style2.jpg";
	string $sample3 = $melPath + "/images/source/source_style3.jpg";
	
	if ( `window -exists ExemplarWindow` )
		deleteUI -window ExemplarWindow;
	if (`iconTextRadioCollection -exists itRadCollection`)
	{
		for ($item in `iconTextRadioCollection -q -collectionItemArray itRadCollection`)
		{
			deleteUI -control $item;
		}
		deleteUI -collection itRadCollection;
	}
		
	window -title "Exemplar Palette" -widthHeight 1000 500 ExemplarWindow;  
	columnLayout -adjustableColumn false;
		rowLayout -numberOfColumns 5;
			iconTextRadioCollection itRadCollection;
			iconTextRadioButton -st "iconOnly" -image1 $sample1 -label "sample1" -w 100 -h 100;
			iconTextRadioButton -st "iconOnly" -image1 $sample2 -label "sample2" -w 100 -h 100;
			iconTextRadioButton -st "iconOnly" -image1 $sample3 -label "sample3" -w 100 -h 100;
		setParent ..;

	formLayout -numberOfDivisions 100 myForm;
		button -label "Add Exemplar" -w 80 -command "AddExemplars()" BtnAdd;
		button -label "Load" -w 50 -command "LoadExemplars()" BtnLoad;
	formLayout -edit
		-attachForm BtnAdd "bottom" 10
		-attachForm BtnAdd "left" 10
        -attachForm BtnLoad "bottom" 10
		-attachForm BtnLoad "left" 120
	myForm;
	showWindow ExemplarWindow; 
}

global proc generateStylit()
{
	global string $exemplarPath;
	global string $lpePath;
	
    sets -add initialShadingGroup StylitShape1;
    createNode StylelitNode -n StylelitNode1;
	setAttr StylelitNode1.source -type "string" $lpePath;
	setAttr StylelitNode1.style -type "string" $exemplarPath;
    connectAttr StylelitNode1.outputMesh StylitShape1.inMesh;
}

global proc StylitGUI()
{
    createNode transform -n Stylit1;
	createNode mesh -n StylitShape1 -p Stylit1;
	global string $melPath;
	global string $exemplarPath;
	$exemplarPath = $melPath + "/images/source/source_style.jpg";
	if ( `window -exists StylitWindow` )
	{
		deleteUI -window StylitWindow;
	}
	window -title "Stylit" -widthHeight 350 600 StylitWindow;    
        columnLayout -adjustableColumn true;
			frameLayout -label "Load LPEs" -collapsable true;
				rowLayout  -numberOfColumns 1 -width 350;
					textField -w 200 -tx "" myLPEs;
				setParent ..;
				rowLayout -numberOfColumns 2 -width 350;    
					button -label "Load" -w 50 -command "browseFiles()";
					button -label "Clear" -w 50 -command "clearLPEs()";
				setParent ..;	
				rowLayout;
					text -label "";
            setParent ..;
			
			frameLayout -label "Select Examplar" -collapsable true;
				rowLayout -numberOfColumns 2 -width 350;
					textField -w 200 -tx "Default" myExemplar;
					button -label "Load" -w 50 -command "ExemplarGUI()";
				setParent ..;
				rowLayout -numberOfColumns 1  -width 350;
					image -image $exemplarPath myExemplarImg;
				setParent ..;
			setParent ..;
			
			rowLayout  -numberOfColumns 1 -width 350;
				button -label "Generate" -w 50 -command "generateStylit()";
			setParent ..;
        setParent ..;
    showWindow StylitWindow; 
}

setParent $gMainWindow;
menu -label "Stylit" - parent $gMainWindow -tearOff on;
menuItem -ann "Styit" -label "Stylit" -command "StylitGUI()";
